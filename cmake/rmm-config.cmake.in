
@PACKAGE_INIT@

cmake_minimum_required(VERSION 3.17)

include(CMakeFindDependencyMacro)
find_dependency(CUDAToolkit)
find_dependency(spdlog 1.7.0)

# Try to find newer Thrust via config first
set(CMAKE_FIND_PACKAGE_PREFER_CONFIG TRUE)
set(_save_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}")

find_dependency(Thrust 1.9.0)

set(CMAKE_MODULE_PATH ${_save_CMAKE_MODULE_PATH})

if (Thrust_CONFIG)
  # we can use the new way of creating a target
  thrust_create_target(rmm::Thrust FROM_OPTIONS)
else()
  # otherwise we have to do it ourselves
  if (NOT TARGET Thrust) 
    add_library(Thrust INTERFACE)
    set_target_properties(Thrust PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES "${THRUST_INCLUDE_DIR}")
  endif()
  add_library(rmm::Thrust ALIAS Thrust)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/rmm-targets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/rmm-config-version.cmake")

check_required_components(rmm)

set(${CMAKE_FIND_PACKAGE_NAME}_CONFIG "${CMAKE_CURRENT_LIST_FILE}")

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(${CMAKE_FIND_PACKAGE_NAME} CONFIG_MODE)
